apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.udacity.gradle.builditbigger"
        minSdkVersion 10
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        free {
            applicationId "com.udacity.gradle.builditbigger.free"
        }
        paid {
            applicationId "com.udacity.gradle.builditbigger.paid"
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    // Added for AdMob
    

    freeCompile project(path: ':jokedisplay', configuration: 'freeRelease')
    paidCompile project(path: ':jokedisplay', configuration: 'paidRelease')
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.google.android.gms:play-services-ads:8.4.0'
    compile project(path: ':backend', configuration: 'android-endpoints')
    androidTestCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
}


task StartServer(dependsOn: ':backend:appengineRun') {
    // Help From stack overflow - http://stackoverflow.com/questions/31434928/android-gradle-task-google-appengine
    project(":backend").afterEvaluate { backend ->
        backend.extensions.appengine.daemon = true
    }
}
task StartTests(dependsOn: 'connectedCheck') {
    mustRunAfter 'StartServer'
}
task RunTests(dependsOn: ['StartServer','StartTests']) {
    finalizedBy ':backend:appengineStop'
}